import VerifiedGarbage.Proof.TripleDes.X86.Loop

namespace VG.Proof.TripleDes.X86
open VG VG.X86 VG.X86.Straight VG.X86.RegUpd VG.Impl.TripleDes.X86
open VG.Spec.TripleDes (Direction)

def scheduleArg (s : State) : BitVec 32 := s.mem.readW (wordAddr (s.gpr .esp) 1) 32

def startAddr (c : Nat) (d : Direction) (s : State) : BitVec 32 :=
  scheduleArg s + BitVec.ofNat 32 (128 * c + if d = .encrypt then 0 else 120)

def startMem (c : Nat) (d : Direction) (s : State) : Mem :=
  (s.mem.writeW (wordAddr (s.gpr .ebp) 4) (startAddr c d s)).writeW
    (wordAddr (s.gpr .ebp) 5) (16 : BitVec 32)

theorem passStart_ok (c : Nat) (d : Direction) (s : State) (hok : Ok sboxCfg s)
    (hr : InRegions (s.rd ++ s.wr) (wordAddr (s.gpr .esp) 1) 4) :
    ∃ s', runBlock isa (passStart c d) s = some s' ∧ s'.mem = startMem c d s ∧
      s'.rd = s.rd ∧ s'.wr = s.wr ∧
      (∀ r, r ≠ .eax → s'.gpr r = s.gpr r) := by
  have hw4 := hok.slotIn 4 (by decide)
  have hw5 := hok.slotIn 5 (by decide)
  simp only [wordAddr, addr, sboxCfg] at hw4 hw5 hr
  refine ⟨_, by
    simp only [passStart, imm, runBlock_cons, runStep_some, runBlock_nil, exec,
      execAlu, readSrc, State.load32, State.store32, State.ea, memOp, hw4, hw5, hr,
      ite_true, Option.bind_some, Option.map_some, gpr_setReg, gpr_arithFlags,
      mem_setReg, mem_arithFlags, rd_setReg, rd_arithFlags, wr_setReg, wr_arithFlags,
      reduceCtorEq, ite_false]
    rfl, ?_, ?_, ?_, ?_⟩
  · rfl
  · rfl
  · rfl
  · intro r hr
    simp only [gpr_setReg, gpr_arithFlags, hr, ite_false]

/-- Starting a DES pass writes only its public pointer and round count. -/
theorem start_frame (c : Nat) (d : Direction) (s : State)
    (fit : (s.gpr .ebp).toNat + 512 ≤ 2 ^ 32) : Frame [workRegion s] s.mem (startMem c d s) := by
  have h4 : (workRegion s).Contains (wordAddr (s.gpr .ebp) 4) 4 := by
    change (workRegion s).Contains (addr (s.gpr .ebp) 16) 4
    rw [addr_eq (by omega)]
    exact Offset.contains _ (by decide) (by decide) (by decide)
  have h5 : (workRegion s).Contains (wordAddr (s.gpr .ebp) 5) 4 := by
    change (workRegion s).Contains (addr (s.gpr .ebp) 20) 4
    rw [addr_eq (by omega)]
    exact Offset.contains _ (by decide) (by decide) (by decide)
  exact ((Frame.refl [workRegion s] s.mem).writeW (List.mem_singleton_self _) _ h4).writeW
    (List.mem_singleton_self _) _ h5

theorem start_pointer (c : Nat) (d : Direction) (s : State) (t : State)
    (base : t.gpr .ebp = s.gpr .ebp) (mem : t.mem = startMem c d s)
    (fit : (s.gpr .ebp).toNat + 512 ≤ 2 ^ 32) :
    roundKeyPtr t = startAddr c d s ∧ roundCount t = 16 := by
  have hsep : Mem.Sep (wordAddr (s.gpr .ebp) 4) 4 (wordAddr (s.gpr .ebp) 5) 4 := by
    intro a h4 h5; exact counter_ptr_sep s fit a h5 h4
  constructor
  · unfold roundKeyPtr
    rw [base, mem, startMem, Mem.readW_writeW_sep hsep (by decide), Mem.readW_writeW_self32]
  · unfold roundCount
    rw [base, mem, startMem, Mem.readW_writeW_self32]
end VG.Proof.TripleDes.X86
